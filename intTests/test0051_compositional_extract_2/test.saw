enable_experimental;

m <- llvm_load_module "test.bc";

let ptr_to_fresh nm ty = do {
    x <- llvm_fresh_var nm ty;
    p <- llvm_alloc ty;
    llvm_points_to p (llvm_term x);
    return (x, p);
};

let add_spec = do {
  r_p <- llvm_alloc (llvm_alias "struct.uint128_t");
  (_, a_p) <- ptr_to_fresh "a" (llvm_alias "struct.uint128_t");
  (_, b_p) <- ptr_to_fresh "b" (llvm_alias "struct.uint128_t");
  llvm_execute_func [r_p, a_p, b_p];
  r <- llvm_fresh_var "r" (llvm_alias "struct.uint128_t");
  llvm_points_to r_p (llvm_term r);
};

let sum_spec n = do {
  s_p <- llvm_alloc (llvm_alias "struct.uint128_t");
  (_, a_p) <- ptr_to_fresh "a" (llvm_array n (llvm_alias "struct.uint128_t"));
  llvm_execute_func [s_p, a_p, (llvm_term {{ `n:[64] }})];
  s <- llvm_fresh_var "s" (llvm_alias "struct.uint128_t");
  llvm_points_to s_p (llvm_term s);
};

add_ov <- llvm_compositional_extract m "add" "add" [] false add_spec yices;
_ <- llvm_compositional_extract m "sum" "sum" [add_ov] false (sum_spec 10) yices;

add_thm <- prove_print yices
  {{ \a b -> add a b == reverse (split ((join (reverse a)) + (join (reverse b)))) }};
prove_print (do { simplify (addsimps [add_thm] empty_ss); simplify (cryptol_ss ()); yices; })
  {{ \a -> sum a == reverse (split (foldl (+) (0:[128]) (map join (map reverse a)))) }};

