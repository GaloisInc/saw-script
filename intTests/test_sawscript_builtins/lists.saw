// Some tests for SAWScript lists.

print "null:";
print (true, null []);
print (false, null [1]);

print "nth:";
print (1, nth [1] 0);
print (4, nth [1, 2, 3, 4, 5] 3);
// Note: need to wrap in a do-block to trap the failure (see #2424)
fails (do { return (nth [1, 2, 3] 4); });
fails (do { return (nth [] 0); });

print "head:";
print (1, head [1]);
print (3, head [3, 4, 5]);
// Note: need to wrap in a do-block to trap the failure (see #2424)
fails (do { return (head []); });

print "tail:";
print ([], tail [1]);
print ([4, 5], tail [3, 4, 5]);
// Note: need to wrap in a do-block to trap the failure (see #2424)
fails (do { return (tail []); });

print "length";
print (0, length []);
print (1, length [1]);
print (5, length [1, 2, 3, 4, 5]);

// note that concat is what's usually called append
print "concat:";
print ([], concat [] []);
print ([1], concat [] [1]);
print ([1], concat [1] []);
print ([2, 1], concat [2] [1]);
print ([2, 4, 6, 8, 1, 2, 3], concat [2, 4, 6, 8] [1, 2, 3]);

print "str_concat:";
print ("", str_concat "" "");
print ("a", str_concat "" "a");
print ("a", str_concat "a" "");
print ("ba", str_concat "b" "a");
print ("defgabc", str_concat "defg" "abc");

// str_concats is what's usually called concat
print ("", str_concats ["", "", ""]);
print ("abc", str_concats ["a", "b", "c"]);

// for
print "*** for (empty) ***";
for [] print;
print "*** for (one) ***";
for [1] print;
print "*** for (two) ***";
for [1, 2] print;
print "*** end for ***";

// implement fold and map (uses the Haskell argument orderings)
rec foldl f z xs = if null xs then z else foldl f (f z (head xs)) (tail xs);
rec foldr f z xs = if null xs then z else f (head xs) (foldr f z (tail xs));
let map f xs = foldl (\acc x -> concat acc [f x]) [] xs;

print "**** map ****";
let inc x = eval_size {| x + 1 |};
print (map inc []);
print (map inc [1, 2]);

print "**** foldl ****";
print (foldl str_concat "zero" []);
print (foldl str_concat "zero" ["alpha"]);
print (foldl str_concat "zero" ["alpha", "beta"]);

print "**** foldr ****";
print (foldr str_concat "zero" []);
print (foldr str_concat "zero" ["alpha"]);
print (foldr str_concat "zero" ["alpha", "beta"]);
