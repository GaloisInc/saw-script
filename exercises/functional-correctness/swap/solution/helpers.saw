// Sawscript utility definitions, inspired by @weaversa's
// https://github.com/GaloisInc/saw-demos/blob/master/common/llvm.saw

/**
 * Creates a fresh symbolic variable with name of specified type,
 * initializes the pointer to the variable's location, and returns
 * the tuple (variable, pointer)
 */
let pointer_to_fresh (type : LLVMType) (name : String) = do {
    x <- llvm_fresh_var name type;
    p <- llvm_alloc type;
    llvm_points_to p (llvm_term x);
    return (x, p);
};

/**
 * Creates a fresh, read-only, symbolic variable with name and type,
 * returns tuple (variable, pointer)
 */
let pointer_to_fresh_readonly (type : LLVMType) (name : String) = do {
    x <- llvm_fresh_var name type;
    p <- llvm_alloc_readonly type;
    llvm_points_to p (llvm_term x);
    return (x, p);
};