/**
 * Cryptol AES property verification.
 *
 * This module efficiently checks that decrypt is the inverse of encrypt.
 *
 * @copyright Galois Inc.
 * @author Eric Mertens <emertens@galois.com>
 */

import "../../deps/cryptol-specs/Common/GF28.cry" as GF28;
import "../../deps/cryptol-specs/Primitive/Symmetric/Cipher/Block/AES/Instantiations/AES256.cry";

let ss0 = cryptol_ss ();

print "Verifying that cipher unrolls";
unroll_cipher <- prove_print
    (w4_unint_z3 ["AddRoundKey", "MixColumns", "SubBytes", "ShiftRows"])
    {{ \w pt -> cipher w pt ==
    (stateToMsg (AddRoundKey (w@14) (ShiftRows (SubBytes (t 13 (t 12 (t 11 (t 10 (t 9 (t 8 (t 7 (t 6 (t 5 (t 4 (t 3 (t 2 (t 1 (AddRoundKey (w@0) (msgToState pt))))))))))))))))))
        where
        t i state = AddRoundKey (w@i) (MixColumns (ShiftRows (SubBytes state))))
    }};

print "Verifying that invCipher unrolls";
unroll_invCipher <- prove_print
    (w4_unint_z3 ["AddRoundKey", "InvMixColumns", "InvSubBytes", "InvShiftRows"])
    {{ \w ct -> invCipher w ct ==
    (stateToMsg (AddRoundKey (w@0) (InvSubBytes (InvShiftRows (t 1 (t 2 (t 3 (t 4 (t 5 (t 6 (t 7 (t 8 (t 9 (t 10 (t 11 (t 12 (t 13 (AddRoundKey (w@14) ( msgToState ct))))))))))))))))))
        where
        t i state = InvMixColumns (AddRoundKey (w@i) (InvSubBytes (InvShiftRows state))))
    }};

print "Verifying that SBox unfolds";
/* This performance trick is necessary because SAW doesn't memoize sboxTable */
unfold_SBox <- prove_print (unint_z3 ["inverse", "add", "rotateR"])
  {{ \i -> (SBox i == GF28::add [b, b >>> 4, b >>> 5, b >>> 6, b >>> 7, 0x63] where b = GF28::inverse i) }};

print "Verifying that InvSBox unfolds";
/* This performance trick is necessary because SAW doesn't memoize sboxInvTable */
unfold_SBoxInv <- prove_print (unint_z3 ["inverse", "add", "rotateR"])
  {{ \i -> SBoxInv i == GF28::inverse (GF28::add [i >>> 2, i >>> 5, i >>> 7, 0x05]) }};

print "Verifying that SBoxInv inverts SBox";
invert_SBox <- prove_print
    do {
        simplify (addsimps [unfold_SBox, unfold_SBoxInv] ss0);
        rme;
    }
    {{ \s -> SBoxInv (SBox s) == s }};

print "Verifying that InvSubBytes inverts SubBytes";
invert_SubBytes <- prove_print
    do {
        unfolding ["InvSubBytes", "SubBytes"];
        simplify (add_prelude_eqs ["map_map"] (addsimps [invert_SBox] ss0));
        rme;
    }
    {{ \s -> InvSubBytes (SubBytes s) == s }};

print "Verifying that InvShiftRows inverts ShiftRows";
invert_ShiftRows <- prove_print rme
    {{ \s -> InvShiftRows (ShiftRows s) == s }};

print "Verifying that InvMixColumns inverts MixColumns";
invert_MixColumns <- prove_print rme
    {{ \s -> InvMixColumns (MixColumns s) == s }};

print "Verifying that msgToState inverts stateToMsg";
invert_stateToMsg <- prove_print rme {{\s -> msgToState (stateToMsg s) == s}};

print "Verifying that stateToMsg inverts msgToState";
invert_msgToState <- prove_print rme {{\s -> stateToMsg (msgToState s) == s}};

print "Verifying that AddRoundKey is involutive";
invert_AddRoundKey <- prove_print rme
    {{ \x y -> AddRoundKey x (AddRoundKey x y) == y }};

print "Verifying that invCipher inverts cipher";
invert_cipher <- prove_print do {
    simplify (addsimps
        [unroll_cipher, unroll_invCipher, invert_ShiftRows,
         invert_MixColumns, invert_SubBytes, invert_msgToState,
         invert_stateToMsg, invert_msgToState, invert_AddRoundKey] ss0);
    rme;
   }
   {{ \w pt -> invCipher w (cipher w pt) == pt }};

print "Verifying that decrypt inverts encrypt";
prove_print do {
    unfolding ["aesIsCorrect", "encrypt", "decrypt"];
    simplify (addsimps [invert_cipher] ss0);
    rme;
   }
   {{ aesIsCorrect }};
