import "imports.cry";

let identity_spec n : CrucibleSetup () = do {
  crucible_execute_func [crucible_term n];
  crucible_return (crucible_term n);
};

let example_spec : CrucibleSetup () = do {
  crucible_execute_func [];
  crucible_return (crucible_term {{3:[32]}});
};

let alloc_init ty v = do {
    p <- crucible_alloc ty;
    crucible_points_to p v;
    return p;
};

let ptr_to_fresh n ty = do {
    x <- crucible_fresh_var n ty;
    p <- alloc_init ty (crucible_term x);
    return (x, p);
};

let sum_spec n : CrucibleSetup() = do {
  (arr,arrp) <- ptr_to_fresh "arr" (llvm_array n (llvm_int 32));

  crucible_execute_func[arrp, crucible_term {{ `n:[32]}}];
  crucible_return (crucible_term {{ mysum arr }});
  crucible_points_to arrp (crucible_term arr);
};

let example_sums : CrucibleSetup() = do {
  crucible_execute_func[];
  crucible_return (crucible_term {{ 70:[32] }});
};

let set_myglobal n : CrucibleSetup() = do {
  crucible_alloc_global "myglobal";
  old <- crucible_fresh_var "old" (llvm_int 32);
  crucible_precond {{ old < 1000 }};
  crucible_points_to (crucible_global "myglobal") (crucible_term old);
  crucible_execute_func[crucible_term {{ `n : [32] }}];
  crucible_points_to (crucible_global "myglobal")
                     (crucible_term {{ `n + old*old : [32] }});
};

let myglobal_example : CrucibleSetup() = do {
  crucible_alloc_global "myglobal";
  crucible_execute_func[];
  crucible_return (crucible_term {{ 120 : [32] }});
  crucible_points_to (crucible_global "myglobal")
                     (crucible_term {{ 120 : [32] }});
};

let main : TopLevel () = do {

    m <- llvm_load_module "source.bc";
    id1 <- crucible_llvm_verify m "identity" [] false (identity_spec {{1:[32]}}) abc;
    id2 <- crucible_llvm_verify m "identity" [] false (identity_spec {{2:[32]}}) abc;
    crucible_llvm_verify m "example"     [id1,id2] false example_spec z3;
    //crucible_llvm_verify "bad_example" [id1,id2] false example_spec (do { print_goal; assume_unsat; });

    sum3  <- crucible_llvm_verify m "sum" [] false (sum_spec  3) z3;
    sum6  <- crucible_llvm_verify m "sum" [] false (sum_spec  6) z3;
    sum10 <- crucible_llvm_verify m "sum" [] false (sum_spec 10) z3;
    sum12 <- crucible_llvm_verify m "sum" [] false (sum_spec 12) z3;
    crucible_llvm_verify m "example_sums" [sum3,sum12, sum6,sum10] false example_sums z3;

    set10 <- crucible_llvm_verify m "add_myglobal" [] false (set_myglobal 10) z3;
    set20 <- crucible_llvm_verify m "add_myglobal" [] false (set_myglobal 20) z3;
    set30 <- crucible_llvm_verify m "add_myglobal" [] false (set_myglobal 30) z3;
    crucible_llvm_verify m "myglobal_example" [set10,set20,set30] false myglobal_example z3;
    crucible_llvm_verify m "myglobal_example" [set20,set30,set10] false myglobal_example z3;
    crucible_llvm_verify m "myglobal_example" [set30,set10,set20] false myglobal_example z3;
    print "done";
};
